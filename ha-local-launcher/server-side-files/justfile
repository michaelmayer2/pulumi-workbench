set dotenv-load

EFS_ID := env_var("EFS_ID")  # For example: 'fs-0ae474bb0403fc7c6'
RSW_LICENSE := env_var("RSW_LICENSE")
NAME := env_var("NAME")
#AD_DOMAIN := env_var("AD_DOMAIN")
#AD_PASSWD := env_var("AD_PASSWD")


# -----------------------------------------------------------------------------
# Build RSW
# -----------------------------------------------------------------------------

# Install RStudio workbench and all of the dependencies
build-rsw: 
    #!/bin/env bash
    # Basic setup
    just install-linux-tools 
    
    # Set up shared drive
    just install-efs-utils
    just mount-efs
    sudo mkdir -p /mnt/efs/rstudio-server/shared-storage
    
    # Add AD integration
    just integrate-ad

    # Install RSW and required dependencies
    just install-r 
    just symlink-r
    just install-rsw
    just generate-cookie-key
    sudo cp -r /etc/rstudio /etc/rstudio-original-conf-files

    # Set up config files
    if [ $NAME -eq 1 ]; then
        just copy-rsw-config-files
        just create-launcher-ssl
    fi
    just setup-rsw-systemctl-overrides 
    just install-launcher-ssl 
    # Restart
    just restart-clean 

# -----------------------------------------------------------------------------
# Helpers
# -----------------------------------------------------------------------------

install-launcher-ssl:
    #!/usr/bin/bash
    sudo apt-get install -y ssl-cert
    sudo usermod --append --groups ssl-cert rstudio-server 

create-launcher-ssl:
    #!/usr/bin/env bash
    set -euxo pipefail
    configdir="/efs/rstudio/etc/rstudio" 
    sudo mkdir -p $configdir
    if [ ! -f $configdir/launcher.pem ]; then 
        sudo openssl genpkey -algorithm RSA -out $configdir/launcher.pem -pkeyopt rsa_keygen_bits:2048
        sudo chown rstudio-server:rstudio-server $configdir/launcher.pem
    fi
    if [ ! -f $configdir/launcher.pub ]; then
        sudo openssl rsa -in $configdir/launcher.pem -pubout > /tmp/launcher.pub
        sudo cp /tmp/launcher.pub $configdir 
        sudo rm -f /tmp/launcher.pub    
        sudo chown rstudio-server:rstudio-server $configdir/launcher.pub
    fi

restart-clean:
    sudo systemctl stop rstudio-server  
    sudo systemctl stop rstudio-launcher 
    sudo rm -rf /var/log/rstudio*
    sudo systemctl start rstudio-launcher
    sudo systemctl start rstudio-server 

restart-server:
    sudo rstudio-server restart

restart-cluster:
    sudo rstudio-server reset-cluster

restart-launcher:
    sudo rstudio-launcher restart

status:
    sudo rstudio-server status

status-load-balancer:
    curl http://localhost:8787/load-balancer/status

logs:
    sudo tail /var/log/rstudio/rstudio-server/rserver.log

list-nodes:
    sudo rstudio-server list-nodes

edit:
    sudo vim /etc/rstudio/rserver.conf

edit-database:
    sudo vim /etc/rstudio/database.conf

edit-load-balancer:
    sudo vim /etc/rstudio/load-balancer

# -----------------------------------------------------------------------------
# Install
# -----------------------------------------------------------------------------

install-linux-tools:
    #!/bin/env bash
    export DEBIAN_FRONTEND=noninteractive 
    sudo -E apt-get update
    sudo -E apt-get install -y tree
    sudo -E apt-get install -y bat ldap-utils
    sudo -E apt-get install -y gdebi-core
    sudo -E apt-get install -y expect 
    echo "alias bat='batcat --paging never'" >> ~/.bashrc

#install-rsw RSW_VERSION='2022.07.0-548.pro5':
install-rsw RSW_VERSION='2023.03.0-386.pro1':
    #!/bin/env bash
    export DEBIAN_FRONTEND=noninteractive
    if [ ! -f rstudio-workbench-{{RSW_VERSION}}-amd64.deb ]; then 
        curl -O https://download2.rstudio.org/server/bionic/amd64/rstudio-workbench-{{RSW_VERSION}}-amd64.deb
    fi 
    sudo -E gdebi -n rstudio-workbench-{{RSW_VERSION}}-amd64.deb 
    sudo rstudio-server license-manager activate {{RSW_LICENSE}}
    rm -f rstudio-workbench-{{RSW_VERSION}}-amd64.deb

install-r r_version='4.2.2':
    #!/bin/env bash
    export DEBIAN_FRONTEND=noninteractive                   
    curl -O https://cdn.rstudio.com/r/ubuntu-2004/pkgs/r-{{r_version}}_1_amd64.deb
    sudo -E gdebi -n r-{{r_version}}_1_amd64.deb 
    rm -f r-{{r_version}}_1_amd64.deb

copy-rsw-config-files:
    #!/bin/env bash
    configdir="/efs/rstudio/etc/rstudio"
    sudo mkdir -p $configdir
    if [ -f ~/rserver.conf ]; then
        sudo cp ~/rserver.conf $configdir/rserver.conf 
        sudo cp ~/load-balancer $configdir/load-balancer 
        sudo cp ~/database.conf $configdir/database.conf
        sudo chmod 0600 $configdir/database.conf
        sudo chown rstudio-server:rstudio-server $configdir/database.conf 
        sudo cp ~/launcher.conf $configdir/launcher.conf
        sudo cp ~/logging.conf $configdir/logging.conf 
        #rm -f ~/rserver.conf ~/load-balancer ~/database.conf ~/launcher.conf 
    fi

setup-rsw-systemctl-overrides:
    #!/bin/env bash
    configdir="/efs/rstudio/etc/rstudio"
    for i in server launcher 
    do
        sudo mkdir -p /etc/systemd/system/rstudio-$i.service.d
        echo "[Service]" | sudo tee -a /etc/systemd/system/rstudio-$i.service.d/override.conf
        echo "Environment=\"RSTUDIO_CONFIG_DIR=$configdir\"" | sudo tee -a /etc/systemd/system/rstudio-$i.service.d/override.conf
    done
    sudo systemctl daemon-reload
# -----------------------------------------------------------------------------
# EFS Mount
# -----------------------------------------------------------------------------

install-efs-utils:
    #!/bin/env bash
    export DEBIAN_FRONTEND=noninteractive                   
    set -euxo pipefail
    sudo -E apt-get -y install binutils
    if ! [ -d efs-utils ]; then 
        git clone https://github.com/aws/efs-utils
    fi
    cd efs-utils
    ./build-deb.sh
    sudo -E apt-get -y install ./build/amazon-efs-utils*deb

set-efs-conf:
    #!/bin/bash
    sudo bash -c 'cat <<EOF >> /etc/fstab
    # mount efs
    {{EFS_ID}}:/ /efs efs defaults,_netdev 0 0
    EOF'

mount-efs:
    sudo mkdir -p /efs;
    sudo mount -t efs -o tls {{EFS_ID}}:/ /efs;
    just set-efs-conf

# -----------------------------------------------------------------------------
# Linux mgmt
# -----------------------------------------------------------------------------

add-user name password:
    #!/bin/bash
    sudo mkdir -p /mnt/efs/home
    sudo useradd --create-home --home-dir /mnt/efs/home/{{name}} -s /bin/bash {{name}};
    echo -e '{{password}}\n{{password}}' | sudo passwd {{name}};

generate-cookie-key:
    #!/bin/env bash
    export DEBIAN_FRONTEND=noninteractive                   
    sudo apt-get update 
    sudo apt-get install -y uuid
    sudo mkdir -p /efs/rstudio/etc/rstudio/
    cookie_key_location="/efs/rstudio/etc/rstudio/secure-cookie-key"
    if [ ! -f ${cookie_key_location} ]; then 
        sudo sh -c "echo `uuid` > ${cookie_key_location}"
        sudo chmod 0600 ${cookie_key_location} 
        sudo chown rstudio-server:rstudio-server ${cookie_key_location}
    fi 

symlink-r r_version='4.2.2':
    #!/bin/bash
    if ! [ -f /usr/local/bin/R ]; then
        sudo ln -s /opt/R/{{r_version}}/bin/R /usr/local/bin/R
        sudo ln -s /opt/R/{{r_version}}/bin/Rscript /usr/local/bin/Rscript
    fi


install-adcli:
    #!/bin/bash
    sudo apt-get update 
    sudo apt install -y git automake libtool libkrb5-dev libldap2-dev libsasl2-dev make
    git clone -b 0.9.2 https://gitlab.freedesktop.org/realmd/adcli.git
    pushd adcli
    ./autogen.sh --disable-doc
    make
    sudo make install
    popd
    rm -rf adcli 

install-ad-prereqs:
    #!/bin/bash
    sudo DEBIAN_FRONTEND=noninteractive apt-get -y install sssd realmd krb5-user samba-common packagekit pamtester

update-etchosts:
    #!/bin/bash
    sudo apt-get install -y net-tools
    echo -e "\n#Local server\n`ifconfig | grep inet | head -1 | awk '{print $2}'` `hostname`" | sudo tee -a /etc/hosts

copy-ad-files:
    #!/bin/bash
    #let's remove the symlink so that systemd does not interfere 
    sudo rm -f /etc/resolv.conf
    sudo cp ~/krb5.conf /etc/
    sudo cp ~/resolv.conf /etc/
    #rm -f ~/krb5.conf ~/resolv.conf

join-ad:
    #!/bin/bash
    echo $AD_PASSWD | sudo realm join -U Administrator $AD_DOMAIN
    echo "ad_gpo_map_service = +rstudio" | sudo tee -a /etc/sssd/sssd.conf
    echo "krb5_canonicalize = True" | sudo tee -a /etc/sssd/sssd.conf
    sudo sed -i 's#use_fully_qualified_names.*#use_fully_qualified_names=False#' /etc/sssd/sssd.conf
    sudo systemctl stop sssd && sudo rm -f /var/lib/sss/db/* && sudo systemctl start sssd
    sudo pam-auth-update --enable mkhomedir

integrate-ad:
    just install-adcli 
    just install-ad-prereqs
    just update-etchosts 
    just copy-ad-files
    just join-ad
